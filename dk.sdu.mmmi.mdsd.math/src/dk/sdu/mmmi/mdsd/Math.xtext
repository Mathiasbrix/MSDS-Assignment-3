grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

MathExp:
	'program' name=ID externals += ExternalDefinition* variables += VarBinding*
;

ExternalDefinition:
	'external' name=ID'(' (parameters += Parameter (',' parameters += Parameter)*)* ')'
;

Parameter:
	name=ID
;

VarBinding:
	'var' name=ID '=' expression=Exp
;

Exp returns Expression:
	Factor (( {Plus.left=current} '+' | {Minus.left=current}  '-' ) right=Factor)*
;

Factor returns Expression:
	Primary (({Mult.left=current} '*' | {Div.left=current}  '/'  ) right=Primary)*
;

Primary returns Expression:
	{MathNumber} value=INT | VariableUse | LetBinding | FunctionCall | {Paranthesis} p=Paranthesis
;

Paranthesis returns Expression:
	'('Exp')'
;

FunctionCall returns Expression:
	{FunctionCall} externalRef=[ExternalDefinition] '(' ( args+=Exp (',' args+=Exp)* )? ')'
;

LetBinding:
	'let' name=ID '=' binding=Exp 'in' body=Exp 'end'
;

Binding:
	VarBinding | LetBinding
;

VariableUse:
	ref = [Binding]
;